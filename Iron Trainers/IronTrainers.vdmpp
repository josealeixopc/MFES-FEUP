class IronTrainers
types
	
values
	private static currentDate = mk_Date(3, 1, 2018);

instance variables
	clients : set of Client;
	trainers : set of Trainer;
	allUsers : set of Utils`MyString;
	user : Utils`MyString;
	
operations

	public IronTrainers : () ==> IronTrainers
	IronTrainers() == (
		clients := {};
		trainers := {};
		allUsers := {};
		user := "undefined";
	);
	
	/*
	*	A 'pure' operation is an operation which does no affect the state of its object nor of any other. It is referentially transparent.
	* A 'read-only' operation, such as a getter, can be considered pure.
	* Making a getter 'pure' is the only way to keep instance variables private and also use their value on pre/post-conditions.
	*/
	
	-- Login in the application
	public login : Utils`MyString ==> bool
	login(name) == 
		if user = "undefined"
		then (
			user := name;
			return true;
		)
		else return false		
	pre len name > 0 and len name < 50;
	
	-- Logout from the application
	public logout : () ==> ()
	logout() ==
		user := "undefined"
	pre user <> "undefined";
			
	-- Returns all users
	pure public getAllUsers : () ==> set of Utils`MyString
	getAllUsers() == return allUsers;
	
	-- Returns all registered clients
	pure public getClients : () ==> set of Client
	getClients() == return clients;
	
	-- Returns all trainers
	pure public getTrainers : () ==> set of Trainer
	getTrainers() == return trainers;

	-- Adds client to registered clients. If can't add client, it returns false
	public addClient : Client ==> ()
	addClient(client) == (
		atomic(
			clients := clients union {client};
			allUsers := allUsers union {client.getEmail()}
		);
	)
	pre client not in set clients and client.getEmail() not in set allUsers
	post client in set clients;
		
	-- Removes client from registered clients
	public removeClient : Client ==> ()
	removeClient(client) == (
		atomic(
			clients := clients \ {client};
			allUsers := allUsers \ {client.getEmail()}
		);
	)
	pre client in set clients and client.getEmail() in set allUsers
	post client not in set clients;
		
	-- Adds trainer to trainers. If can't add trainer, it returns false
	public addTrainer : Trainer ==> ()
	addTrainer(trainer) == (
		atomic(
			trainers := trainers union {trainer};
			allUsers := allUsers union {trainer.getEmail()};
		);
	)
	pre trainer not in set trainers and trainer.getEmail() not in set allUsers
	post trainer in set trainers;
		
	-- Removes trainer from trainers
	public removeTrainer : Trainer ==> ()
	removeTrainer(trainer) ==
	(
		atomic(
			trainers := trainers \ {trainer};
			allUsers := allUsers \ {trainer.getEmail()};
		);
	)
	pre ({trainer} subset trainers) = true and trainer.getEmail() in set allUsers
	post ({trainer} subset trainers) = false;
		
functions
-- TODO Define functiones here
traces
-- TODO Define Combinatorial Test Traces here
end IronTrainers